buildscript {
	ext {
		groovyVersion = '2.5.10'
		restAssuredVersion = '4.2.0'
		springfoxVersion = '2.9.2'
		spockVersion = '1.3-groovy-2.5'
		wiremockVersion = '2.26.3'
		jsoupVersion = '1.13.1'
		zip4jVersion = '2.5.2'
		reflectionsVersion = '0.9.9'
		feignVersion = '2.2.2.RELEASE'
		awaitilityVersion = '4.0.2'
		testcontainersVersion = '1.14.1'
		archunitVersion = '0.13.1'
		chronicleMapVersion = '3.19.4'
		postgresJdbcVersion = '42.2.12'
	}
}

plugins {
	id "org.sonarqube" version "2.8"
	id 'org.springframework.boot' version '2.2.6.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'com.palantir.docker' version '0.25.0'
	id 'java'
	id 'groovy'
	id 'idea'
	id "jacoco"
}

group = 'com.pakisoft'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation "org.springframework.cloud:spring-cloud-starter-openfeign:$feignVersion"
	implementation "org.jsoup:jsoup:$jsoupVersion"
	implementation "net.lingala.zip4j:zip4j:$zip4jVersion"
	implementation "org.reflections:reflections:$reflectionsVersion"
	implementation "io.springfox:springfox-swagger2:$springfoxVersion"
	implementation "io.springfox:springfox-swagger-ui:$springfoxVersion"
	implementation "net.openhft:chronicle-map:$chronicleMapVersion"
	implementation "org.postgresql:postgresql:$postgresJdbcVersion"

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation "org.codehaus.groovy:groovy-all:$groovyVersion"
	testImplementation "com.tngtech.archunit:archunit-junit4:$archunitVersion"
	testImplementation "org.spockframework:spock-core:$spockVersion"
	testImplementation "org.spockframework:spock-spring:$spockVersion"
	testImplementation "com.github.tomakehurst:wiremock:$wiremockVersion"
	testImplementation "io.rest-assured:rest-assured:$restAssuredVersion"
	testImplementation "io.rest-assured:json-path:$restAssuredVersion"
	testImplementation "io.rest-assured:xml-path:$restAssuredVersion"
	testImplementation "org.awaitility:awaitility:$awaitilityVersion"
	testImplementation "org.testcontainers:spock:$testcontainersVersion"
	testImplementation "org.testcontainers:postgresql:$testcontainersVersion"
}

sourceSets {
	test {
		groovy {
			srcDir 'src/test'
		}
	}
}

idea {
	module {
		testSourceDirs += file('src/test/groovy/unit')
		testSourceDirs += file('src/test/groovy/integration')
		testSourceDirs += file('src/test/groovy/functional')
	}
}

sonarqube {
	properties {
		property "sonar.projectKey", "devus10_word-finder"
		property "sonar.sources", "src/main"
		property "sonar.tests", "src/test"
		property "sonar.test.inclusions", "**/*test*/**"
		property "sonar.exclusions", "**/*test*/**"
	}
}

jacocoTestReport {
	reports {
		xml.enabled true
	}
}

plugins.withType(JacocoPlugin) {
	tasks["test"].finalizedBy 'jacocoTestReport'
}

docker {
	name 'words-finder:1.0'
	copySpec.from('build/libs').into('.')
	copySpec.from('docker').into('.')
	buildArgs([JAR_FILE: 'word-finder.jar'])
}
